// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/String.jack
/**
 * Represents character strings. In addition for constructing and disposing
 * strings, the class features methods for getting and setting individual
 * characters of the string, for erasing the string's last character,
 * for appending a character to the string's end, and more typical
 * string-oriented operations.
 */
class String {
    field Array chars;
    field int maxLen;
    field int length;

    /** constructs a new empty string with a maximum length of maxLength
     *  and initial length of 0. */
    constructor String new(int maxLength) {
        if (maxLength = 0) {
            let maxLength = 1;
        }

        let length = 0;
        let maxLen = maxLength;
        let chars = Array.new(maxLength);
        return this;
    }

    /** Disposes this string. */
    method void dispose() {
        do chars.dispose();
        return;
    }

    /** Returns the current length of this string. */
    method int length() {
        return length;
    }

    /** Returns the character at the j-th location of this string. */
    method char charAt(int j) {
        if (j < length) {
            return chars[j];
        } else {
            do Sys.error(17);
        }
        return 48;
    }

    /** Sets the character at the j-th location of this string to c. */
    method void setCharAt(int j, char c) {
        if (j < length) {
            let chars[j] = c;
        } else {
            do Sys.error(17);
        }
        return;
    }

    /** Appends c to this string's end and returns this string. */
    method String appendChar(char c) {
        if (length < maxLen) {
            let chars[length] = c;
            let length = length + 1;
        } else {
            do Sys.error(17);
        }
        return this;
    }

    /** Erases the last character from this string. */
    method void eraseLastChar() {
        if (length > 0) {
            let length = length - 1;
        }
        return;
    }

    /** Returns the integer value of this string, 
     *  until a non-digit character is detected. */
    method int intValue() {
        var int intVal, index;
        var boolean isNegative;

        let intVal = 0;

        if ((length > 0) & (chars[0] = 45)) {
            let isNegative = true;
            let index = 1;
        } else {
            let isNegative = false;
            let index = 0;
        }

        while ((index < length) & String.isDigit(chars[index])) {
            let intVal = (intVal * 10) + (chars[index] - 48);
            let index = index + 1;
        }

        if (isNegative) {
            return -intVal;
        } else {
            return intVal;
        }
    }

    /** Sets this string to hold a representation of the given value. */
    method void setInt(int val) {
        let length = 0;

        if (val < 0) {
            let val = -val;
            do appendChar(45);
        }

        do writeNumberToString(val);
        return;
    }

    method void writeNumberToString(int number) {
        var int frontDigits, lastDigit;
        var char lastDigitChar;

        let frontDigits = number / 10;
        let lastDigit = number - (frontDigits * 10);
        let lastDigitChar = lastDigit + 48;

        if (number < 10) {
            do appendChar(lastDigitChar);
        } else {
            do writeNumberToString(frontDigits);
            do appendChar(lastDigitChar);
        }

        return;
    }

    /** Returns the new line character. */
    function char newLine() {
        return 128;
    }

    /** Returns the backspace character. */
    function char backSpace() {
        return 129;
    }

    /** Returns the double quote (") character. */
    function char doubleQuote() {
        return 34;
    }

    function boolean isDigit(char c) {
        return (c > 47) & (c < 58);
    }
}
